#+STARTIP: overview
Rebinding of basic commands:
| Key   | Command                         |
|-------+---------------------------------|
| C-s   | Save                            |
| C-o   | Switch window                   |
| C-w   | Backward-delete word or region  |
| M-j   | Backward word                   |
| M-k   | Forward word                    |
| C-j   | Backward char                   |
| C-k   | Forward char                    |
| C-b   | Kill (C-k is overwritten)       |
| C-f   | M-x (it's freed, why not?)      |
| C-q   | Search (q for query)            |
| C-z   | Undo                            |
| C-r   | Redo                            |
| C-x t | Beginning (t for top) of buffer |
| C-x e | End of buffer                   |
| M-p   | Scroll up half page             |
| M-n   | Scroll down half page           |
| M-c   | Copy (kill-ring-save)           |
| M-v   | Paste (yank)                    |

* Avy
Use less mouse.
#+begin_src emacs-lisp
  (use-package avy
    :ensure t
    :defer t
    :bind
    ("M-s" . avy-goto-char-timer)
    :custom
    (avy-timeout-seconds 0.3)
    (avy-style 'pre)
    :custom-face
    (avy-lead-face ((t (:background "#ADD8E6" :foreground "#E0115F" :weight bold)))))
#+end_src
* Backup
Haven't found it useful. Maybe you would sleep better at night.
#+begin_src emacs-lisp
  (setq backup-directory-alist `(("." . "~/.saves")))
  (setq auto-save-list-file-prefix "~/.saves/.saves-")
  (setq backup-by-copying t)
  (setq delete-old-versions t
    kept-new-versions 20
    kept-old-versions 10
    version-control t)
#+end_src
* Beacon
Make switching window easier to see.
#+begin_src emacs-lisp
  (use-package beacon
    :ensure t
    :config
    (beacon-mode 1))
#+end_src
* Company
Only basic code completion. (Is that even too much?) No heavy LSP stuffs.
#+begin_src emacs-lisp
  (use-package company
    :ensure t
    :config
    (add-hook 'prog-mode-hook 'company-mode)
    (remove-hook 'text-mode-hook 'company-mode)
    ;;  `company-debbrev` gives odd candidates.
    (delete 'company-dabbrev company-backends)
    :custom
    (company-minimum-prefix-length 3)
    (company-tooltip-align-annotations t)
    (company-require-match 'never)
    ;; Don't use company in the following modes.
    (company-global-modes
     '(not shell-mode eaf-mode text-mode-hook latex-mode-hook))
    ;; Trigger completion immediately.
    (company-idle-delay 0.1)
    ;; Number the candidates (use M-1, M-2 etc to select completions).
    (company-show-numbers t))

  ;; M-/ for Hippe expand.
  (setq hippie-expand-try-functions-list '(try-expand-dabbrev
					   try-expand-dabbrev-all-buffers
					   try-expand-dabbrev-from-kill
					   try-complete-file-name-partially
					   try-complete-file-name
					   try-expand-all-abbrevs
					   try-expand-list
					   try-expand-line
					   try-complete-lisp-symbol-partially
					   try-complete-lisp-symbol))
  (global-set-key [remap dabbrev-expand] 'hippie-expand)
#+end_src
* Crux
Those are useful.
#+begin_src emacs-lisp
  (use-package crux
    :ensure t
    :bind
    (("C-b" . crux-smart-kill-line)
     ("C-x K" . crux-kill-other-buffers)
     ("C-x <delete>" . crux-kill-whole-line)
     ("C-x DEL" . crux-kill-whole-line)
     ("C-c d" . crux-duplicate-current-line-or-region)
     ("C-c q" . crux-duplicate-and-comment-current-line-or-region)
     ("C-x 4 t" . crux-transpose-windows)
     ("C-x C-t" . crux-visit-shell-buffer)
     ("C-<return>" . crux-smart-open-line)
     ("C-RET" . crux-smart-open-line)
     ("M-<return>" . crux-smart-open-line-above)
     ("M-RET" . crux-smart-open-line-above))
    :config
    (crux-with-region-or-buffer indent-region)
    (crux-with-region-or-buffer untabify)
    (crux-with-region-or-line comment-or-uncomment-region) ; (Un)commenting works on line.
    (crux-with-region-or-point-to-eol kill-ring-save)
    (defalias 'rename-file-and-buffer 'crux-rename-file-and-buffer)
    :init
    (global-set-key (kbd "M-;") 'comment-or-uncomment-region)
    (global-set-key (kbd "C-/") 'comment-or-uncomment-region))
#+end_src
* Dired
#+begin_src emacs-lisp
  (use-package dired
    :ensure nil
    :bind
    (("C-x C-j" . dired-jump))
    :custom
    ;; Always delete and copy recursively.
    (dired-listing-switches "-lah")
    (dired-recursive-deletes 'always)
    (dired-recursive-copies 'always)
    ;; Auto refresh Dired, but be quiet about it.
    (global-auto-revert-non-file-buffers t)
    (auto-revert-verbose nil)
    ;; Quickly copy/move file in Dired.
    (dired-dwim-target t)
    ;; Move files to trash when deleting.
    (delete-by-moving-to-trash t)
    ;; Load the newest version of a file.
    (load-prefer-newer t)
    ;; Detect external file changes and auto refresh file.
    (auto-revert-use-notify nil)
    ;; Auto revert every 3 sec.
    (auto-revert-interval 3)
    :config
    ;; Reuse same dired buffer, to prevent numerous buffers while navigating in
    ;; dired.
    (put 'dired-find-alternate-file 'disabled nil)
    :hook
    (dired-mode . (lambda ()
		    (local-set-key (kbd "<mouse-2>")
				   'dired-find-alternate-file)
		    (local-set-key (kbd "RET") #'dired-find-alternate-file)
		    (local-set-key (kbd "^")
				   (lambda () (interactive)
				     (find-alternate-file ".."))))))
#+end_src
* Dumb Jump
Useful to go to code definitions. Old packages that might not work in the
future. Why doesn't Emacs have better go-to-definition packages/commands? It
should be a default feature.
#+begin_src emacs-lisp
(use-package dumb-jump
  :ensure t
  :custom
  (dumb-jump-selector 'ivy)
  (xref-show-definitions-function 'xref-show-definitions-completing-read)
  :bind
  (:map prog-mode-map
    ("C-c C-o" . dumb-jump-go-other-window)
    ("C-c C-y" . dumb-jump-back))
  :config
  (add-hook 'xref-backend-functions 'dumb-jump-xref-activate))
#+end_src emacs-lisp
* Ediff
#+begin_src emacs-lisp
(use-package ediff
  :custom
  (ediff-split-window-function 'split-window-horizontally)
  (ediff-window-setup-function 'ediff-setup-windows-plain))
#+end_src
* Expand Region
Useful in coding.
#+begin_src emacs-lisp
  (use-package expand-region
    :ensure t
    :config
    (global-set-key (kbd "M-e") 'er/expand-region))
#+end_src
* Flycheck
Can be slow.
#+begin_src emacs-lisp
  (use-package flycheck
    :defer t
    :diminish
    :hook (after-init . global-flycheck-mode)
    :commands (flycheck-add-mode)
    :custom
    (flycheck-global-modes
     '(not outline-mode diff-mode shell-mode eshell-mode term-mode))
    (flycheck-emacs-lisp-load-path 'inherit)
    (flycheck-indication-mode (if (display-graphic-p)
				  'right-fringe 'right-margin))
    :bind
    (:map flycheck-mode-map
	  ("C-c p" . flycheck-previous-error)
	  ("C-c n" . flycheck-next-error))
    :init
    (add-hook 'prog-mode-hook 'flycheck-mode))
#+end_src
* Font
#+begin_src emacs-lisp
  (set-frame-font "consolas 12" nil t)

  ;; UTF-8.
  (set-selection-coding-system 'utf-8)
  (prefer-coding-system 'utf-8)
  (set-language-environment "UTF-8")
  (set-default-coding-systems 'utf-8)
  (set-terminal-coding-system 'utf-8)
  (set-keyboard-coding-system 'utf-8)
  (setq locale-coding-system 'utf-8)

  ;; Treat clipboard input as UTF-8 string first; compound text next, etc.
  (when (display-graphic-p)
    (setq x-select-request-type '(UTF8_STRING COMPOUND_TEXT TEXT STRING)))
#+end_src
* Goto-chg
Old package; might not work in the future. Such a basic function that should be
default.
#+begin_src emacs-lisp
  (use-package goto-chg
    :ensure t
    :init
    (global-set-key (kbd "C-c b") 'goto-last-change))
#+end_src emacs-lisp
* Gptel
To prove I work on AI.
#+begin_src emacs-lisp
  (use-package gptel
    :ensure t
    :init
    (setq gptel-model "claude-3-5-sonnet-20240620"
	  gptel-backend (gptel-make-anthropic "Claude"
			  :stream t
			  :key (with-temp-buffer
				 (insert-file-contents "~/.emacs.d/keys/claude.txt")
				 (string-trim (buffer-string)))))
    (add-hook 'gptel-post-stream-hook 'gptel-auto-scroll)
    (add-hook 'gptel-post-response-functions 'gptel-end-of-response)
    (add-hook 'gptel-mode-hook (lambda () (auto-fill-mode -1)))
    :config
    (define-key gptel-mode-map (kbd "C-c C-<return>") 'gptel-send)
    (define-key gptel-mode-map (kbd "C-c C-RET") 'gptel-send))
#+end_src

* Highlight
#+begin_src emacs-lisp
  ;; Highlight indent.
  (use-package highlight-indent-guides
    :ensure t
    :init
    (add-hook 'prog-mode-hook 'highlight-indent-guides-mode)
    :custom
    (highlight-indent-guides-method 'character)
    (highlight-indent-guides-auto-enabled nil)
    (highlight-indent-guides-responsive 'top)
    (highlight-indent-guides-delay 0))

  ;; Highlight current word.
  (use-package symbol-overlay
    :ensure t
    :init
    (add-hook 'prog-mode-hook 'symbol-overlay-mode))
#+end_src
* Ibuffer
#+begin_src emacs-lisp
  (use-package ibuffer
    :ensure t
    :init
    (use-package ibuffer-vc
      :commands (ibuffer-vc-set-filter-groups-by-vc-root)
      :custom
      (ibuffer-vc-skip-if-remote 'nil))
    (global-set-key (kbd "C-x i") 'ibuffer)
    :custom
    (ibuffer-formats
     '((mark modified read-only locked " "
	     (name 35 35 :left :elide)
	     " "
	     (size 9 -1 :right)
	     " "
	     (mode 16 16 :left :elide)
	     " " filename-and-process)
       (mark " "
	     (name 16 -1)
	     " " filename))))
#+end_src
* Ivy
Useful because Emacs default completion sucks.
#+begin_src emacs-lisp
  ;; https://github.com/MatthewZMD/.emacs.d?tab=readme-ov-file#ivy-amx-counsel-swiper.
  (use-package ivy
    :ensure t
    :diminish
    :init
    (use-package amx :ensure t :defer t)
    (use-package counsel :ensure t :diminish :config (counsel-mode 1))
    (use-package swiper :ensure t :defer t)
    (ivy-mode 1)
    :bind
    (("C-f" . 'counsel-M-x)   ; Because C-f is freed.
     ("C-q" . swiper-isearch)
     ("C-c s" . swiper-isearch-thing-at-point)
     ("C-c f" . counsel-recentf)
     ("C-x f" . counsel-recentf)
     ("C-c g" . counsel-rg)
     ("C-y" . counsel-yank-pop)
     ("C-c i" . counsel-imenu)
     ("C-x C-b" . ivy-switch-buffer) ; Additional binding when mispressing `C-x b`.
     (:map ivy-minibuffer-map
	   ("M-RET" . ivy-immediate-done)
	   ("M-v" . yank))) ; yank works in mini buffer.
    :custom
    (ivy-display-style 'fancy)
    (ivy-height 10)
    (ivy-on-del-error-function nil)
    (ivy-magic-slash-non-match-action 'ivy-magic-slash-non-match-create)
    (ivy-count-format "%d/%d")
    (ivy-wrap t)
    ;; `counsel-M-x` prompt doesn't start with `^` to match only beginning of string.
    (ivy-initial-inputs-alist nil))
#+end_src
* Magit
Make git slightly less painful, but git is still a pain in the ass.
#+begin_src emacs-lisp
  (use-package magit
    :ensure t
    :bind
    (("C-x g" . magit-status)
    (:map magit-status-mode-map
    ("M-RET" . magit-diff-visit-file-other-window)))
    :config
    (defun magit-log-follow-current-file ()
    "A wrapper around `magit-log-buffer-file' with `--follow' argument."
    (interactive)
    (magit-log-buffer-file t))
    (setq magit-log-arguments '("-n256" "--graph" "--decorate" "--color")
    ;; Show diff per word.
    magit-diff-refine-hunk t))

  (use-package diff-hl
    :ensure t
    :config
    (global-diff-hl-mode)
    (add-hook 'dired-mode-hook 'diff-hl-dired-mode)
    (add-hook 'magit-post-refresh-hook 'diff-hl-magit-post-refresh)
    (add-hook 'prog-mode-hook 'diff-hl-mode)
    :bind
    (:map diff-hl-mode-map ("<left-fringe> <mouse-1>" . diff-hl-diff-goto-hunk))
    :hook
    ((magit-post-refresh . diff-hl-magit-post-refresh)
     (after-init . global-diff-hl-mode)
     (dired-mode . diff-hl-dired-mode)))
#+end_src emacs-lisp
* Marginalia
Should be Emacs default.
#+begin_src emacs-lisp
  (use-package marginalia
    :ensure t
    :custom
    (marginalia-max-relative-age 0)
    (marginalia-align 'left)
    :bind (:map minibuffer-local-map
	   ("M-A" . marginalia-cycle))
    :init
    (marginalia-mode))
#+end_src
* Multi-cursor
Useful but can waste time to show off (though even temptation is greater in
keyboard macros).
#+begin_src emacs-lisp
  (use-package multiple-cursors
    :ensure t
    :init
    (global-set-key (kbd "C-c <return>") 'mc/edit-lines)
    (global-set-key (kbd "C-c RET") 'mc/edit-lines))
#+end_src
* Mwim
Quite useful. Agreed by Stevey.
#+begin_src emacs-lisp
  (use-package mwim
    :ensure t
    :init
    (global-set-key (kbd "C-a") 'mwim-beginning)
    (global-set-key (kbd "C-e") 'mwim-end))
#+end_src
* Org
Org is great as long as you don't spend your life on it.
#+begin_src emacs-lisp
  (use-package org
    :ensure nil
    :defer t
    :bind
    (:map org-mode-map
	  ("C-i" . nil)
	  ("M-h" . nil)
	  ("C-j" . nil)
	  ("C-<return>" . nil) ; Don't overwrite my open line.
	  ("C-RET" . nil)      ; Don't overwrite my open line.
	  ("M-<return>" . nil) ; Don't overwrite my open line above.
	  ("M-RET" . nil)      ; Don't overwrite my open line above.
	  ;; Don't know why tab doesn't fold/unfold heading if using
	  ;; `use-package` to config org setting (it works if org
	  ;; setting is config without `use-package`).
	  ("TAB" . org-cycle))
    :config
    (org-babel-do-load-languages
     'org-babel-load-languages
     '((C . t)
       (python . t)))
    :custom
    (org-src-tab-acts-natively t)      ; Make tab work in Org-mode code block.
    (org-src-fontify-natively t)       ; Highlight code block.
    (org-confirm-babel-evaluate 'nil)  ; Don't ask when running code.
    :init
    (add-hook 'org-mode-hook 'visual-line-mode))
#+end_src
* Parens
#+begin_src emacs-lisp
  (use-package smartparens
    :ensure t
    :hook (prog-mode . smartparens-mode)
    :diminish smartparens-mode
    :bind
    (:map smartparens-mode-map
	  ("C-M-f" . sp-forward-sexp)
	  ("C-M-b" . sp-backward-sexp))
    :custom
    (sp-escape-quotes-after-insert nil)
    (sp-autoinsert-pair nil) ; Don't auto-insert paired delimiters.
    :config
    ;; Stop pairing single quotes in elisp.
    (sp-local-pair 'emacs-lisp-mode "'" nil :actions nil)
    (sp-local-pair 'org-mode "[" nil :actions nil))

  (use-package rainbow-delimiters
    :ensure t
    :init
    (add-hook 'prog-mode-hook 'rainbow-delimiters-mode))
#+end_src
* Prog
** Tree-sitter
Newly supported. Can be buggy.
#+begin_src emacs-lisp
  (use-package treesit
    :ensure nil
    :init
    (setq treesit-language-source-alist
	  '((bash "https://github.com/tree-sitter/tree-sitter-bash")
	    (cmake "https://github.com/uyha/tree-sitter-cmake")
	    (css "https://github.com/tree-sitter/tree-sitter-css")
	    (elisp "https://github.com/Wilfred/tree-sitter-elisp")
	    (go "https://github.com/tree-sitter/tree-sitter-go")
	    (html "https://github.com/tree-sitter/tree-sitter-html")
	    (javascript "https://github.com/tree-sitter/tree-sitter-javascript" "master" "src")
	    (json "https://github.com/tree-sitter/tree-sitter-json")
	    (make "https://github.com/alemuller/tree-sitter-make")
	    (markdown "https://github.com/ikatyang/tree-sitter-markdown")
	    (python "https://github.com/tree-sitter/tree-sitter-python")
	    (toml "https://github.com/tree-sitter/tree-sitter-toml")
	    (tsx "https://github.com/tree-sitter/tree-sitter-typescript" "master" "tsx/src")
	    (typescript "https://github.com/tree-sitter/tree-sitter-typescript" "master" "typescript/src")
	    (yaml "https://github.com/ikatyang/tree-sitter-yaml")))
    ;; Hacky. https://www.masteringemacs.org/article/how-to-get-started-tree-sitter
    (setq major-mode-remap-alist
	  '((yaml-mode . yaml-ts-mode)
	    (bash-mode . bash-ts-mode)
	    (js2-mode . js-ts-mode)
	    (typescript-mode . typescript-ts-mode)
	    (json-mode . json-ts-mode)
	    (css-mode . css-ts-mode)
	    (python-mode . python-ts-mode)))
    :bind
    ("C-c l ," . treesit-beginning-of-defun)
    ("C-c l ." . treesit-end-of-defun))
#+end_src

** Formatter
#+begin_src emacs-lisp
(use-package apheleia
  :ensure t
  :bind ("C-c l f" . apheleia-format-buffer)
  :config
  (setf (alist-get 'python-ts-mode apheleia-mode-alist) '(isort black)))
#+end_src

** LSP Client
Is LSP good or bad? Does it make me lazy?
#+begin_src emacs-lisp
  (use-package eglot
    :ensure t
    :config
    (add-to-list 'eglot-server-programs '(python-mode . ("pyright-langserver" "--stdio")))
    (add-to-list 'eglot-server-programs '(python-ts-mode . ("pyright-langserver" "--stdio")))
    (add-hook 'python-mode-hook 'eglot-ensure)
    (add-hook 'python-ts-mode-hook 'eglot-ensure)
    (setq read-process-output-max (* 1024 1024))
    (setq eglot-events-buffer-size 0)
    ;; Disable useless LSP UI. https://emacs-lsp.github.io/lsp-mode/tutorials/how-to-turn-off
    (setq lsp-headerline-breadcrumb-enable nil)
    (setq lsp-ui-sideline-enable nil)
    (setq lsp-ui-sideline-enable nil)
    (setq lsp-modeline-code-actions-enable nil)
    (setq lsp-modeline-diagnostics-enable nil)
    :bind
    ("C-c l r" . eglot-rename)
    ("C-c l e" . eldoc)
    :custom (eglot-report-progress nil))
#+end_src

** Python
#+begin_src emacs-lisp
  (use-package python-mode
    :ensure nil
    :after flycheck
    :mode "\\.py\\'"
    :bind
    ;; Annoying you need to do keybinding twice to treesitter mode too.
    ;; When will they fix the hacky way of using ts mode?
    (:map python-mode-map
	  ("C-c C-f" . nil))
    (:map python-ts-mode-map
	  ("C-c C-f" . nil))
    :custom
    (python-indent-offset 4)
    (flycheck-python-pycompile-executable "/usr/local/bin/python3")
    (python-shell-interpreter "/usr/local/bin/python3")
    (py-python-command "/usr/local/bin/python3")
    (python-shell-interpreter "/usr/local/bin/python3")
    (python-indent-guess-indent-offset-verbose nil))
#+end_src

LSP server.
#+begin_src emacs-lisp
  (use-package lsp-pyright
    :ensure t
    :hook
    (python-mode . (lambda ()
		     (require 'lsp-pyright)
		     (lsp)))
    (python-ts-mode . (lambda ()
			(require 'lsp-pyright)
			(lsp))))
#+end_src
* Projectile
Shouldn't be needed if GUI Emacs can isolate buffers in each instance.
#+begin_src emacs-lisp
  (use-package projectile
    :ensure t
    :after ivy
    :bind (:map projectile-mode-map
		("C-x p" . projectile-command-map))
    :custom
    (projectile-completion-system 'ivy)
    :init
    (projectile-mode +1)
    (setq projectile-switch-project-action 'projectile-dired)
    (setq projectile-find-dir-includes-top-level t))
#+end_src

* Recentf
#+begin_src emacs-lisp
  (use-package recentf
    :ensure nil
    :hook (after-init . recentf-mode)
    :custom
    (recentf-auto-cleanup "05:00am")
    (recentf-max-saved-items 200)
    (recentf-exclude '((expand-file-name package-user-dir)
		       ".cache"
		       ".cask"
		       ".elfeed"
		       "bookmarks"
		       "cache"
		       "ido.*"
		       "persp-confs"
		       "recentf"
		       "undo-tree-hist"
		       "url"
		       "COMMIT_EDITMSG\\'")))

  ;; When buffer is closed, saves the cursor location.
  (save-place-mode 1)

  ;; Set history-length longer.
  (setq-default history-length 500)
#+end_src
* Scroll
#+begin_src emacs-lisp
  ;; Scroll window up/down by half window. Default Emacs scolling sucks.
  (use-package view
    :ensure nil
    :init
    (global-set-key (kbd "C-<down>") 'View-scroll-half-page-forward)
    (global-set-key (kbd "C-<up>") 'View-scroll-half-page-backward)
    (bind-key* "M-n" 'View-scroll-half-page-forward)
    (bind-key* "M-p" 'View-scroll-half-page-backward))
    ;; (global-set-key (kbd "M-n") 'View-scroll-half-page-forward)
    ;; (global-set-key (kbd "M-p") 'View-scroll-half-page-backward))

  ;; Vertical Scroll.
  (setq scroll-step 1)
  (setq scroll-margin 1)
  (setq scroll-conservatively 100000)
  (setq scroll-up-aggressively 0.01)
  (setq scroll-down-aggressively 0.01)
  (setq auto-window-vscroll nil)
  (setq fast-but-imprecise-scrolling nil)
  (setq mouse-wheel-scroll-amount '(1 ((shift) . 1)))
  (setq mouse-wheel-progressive-speed nil)

  ;; Horizontal Scroll.
  (setq hscroll-step 1)
  (setq hscroll-margin 1)
  (setq scroll-preserve-screen-position t)

  ;; Click and scroll in terminal.
  (unless (display-graphic-p)
    (xterm-mouse-mode 1) ; Activate mouse-based scrolling.
    (global-set-key (kbd "<mouse-4>") 'scroll-down-line)
    (global-set-key (kbd "<mouse-5>") 'scroll-up-line))
#+end_src
* Shell/Terminal
#+begin_src emacs-lisp
  (use-package exec-path-from-shell
    :ensure t
    :if (memq window-system '(mac ns x))
    :init
    (setq explicit-shell-file-name "/bin/zsh")
    :config
    (exec-path-from-shell-initialize))

  (use-package term
    :ensure nil
    :bind
    (:map term-raw-map
	  ("C-y" . term-paste)
	  ("M-v" . term-paste)
          ("C-o" . other-window)))
#+end_src
* Spell Checker
#+begin_src emacs-lisp
  (use-package flyspell
    :ensure nil
    :diminish
    :if (executable-find "hunspell")
    :hook (((text-mode outline-mode
	     latex-mode org-mode markdown-mode) . flyspell-mode))
    :init
    (add-hook 'prog-mode-hook 'flyspell-prog-mode)
    :custom
    (flyspell-issue-message-flag nil)
    (ispell-program-name "/usr/local/bin/hunspell")
    (ispell-hunspell-dict-paths-alist
      '(("en_US" "/Applications/dict-en-20230701_lo/en_US.aff")))
    (ispell-local-dictionary "en_US")
    (ispell-local-dictionary-alist
    ;; Please note the list `("-d" "en_US")` contains ACTUAL parameters
    ;; passed to hunspell. You could use `("-d" "en_US,en_US-med")` to check
    ;; with multiple dictionaries.
    '(("en_US" "[[:alpha:]]" "[^[:alpha:]]" "[']" nil ("-d" "en_US") nil utf-8)))
    :config
    (use-package flyspell-correct-ivy
      :after ivy
      :ensure t
      :bind
      (:map flyspell-mode-map
	    ([remap flyspell-correct-word-before-point] .
	      flyspell-correct-wrapper)
	    ("M-l" . flyspell-correct-wrapper))
      :custom (flyspell-correct-interface 'flyspell-correct-ivy)))
#+end_src
* Super-save
Make the paranoid sleep better.
#+begin_src emacs-lisp
  (use-package super-save
    :ensure t
    :config
    (super-save-mode +1))
#+end_src
* TeX
#+begin_src emacs-lisp
  (use-package tex
    :ensure auctex
    :defer t
    :init
    (add-to-list 'auto-mode-alist '("\\.tex\\'" . LaTeX-mode))
    (add-hook 'LaTeX-mode-hook 'visual-line-mode)
    (add-hook 'tex-mode-hook 'visual-line-mode)
    :bind
    (:map tex-mode-map
	  ("C-j" . nil))
    (:map LaTeX-mode-map
	  ("C-j" . nil)))
#+end_src
* Theme
Tune Zenburn theme.
#+begin_src emacs-lisp
  (use-package zenburn-theme
    :ensure t
    :config
    (setq zenburn-override-colors-alist
	  ;; Make main background lighter.
	  '(("zenburn-bg" . "#494949")
	    ;; Make main text light gold. Color borrowed from Jon Blow.
	    ("zenburn-fg"  . "#CBB390")
	    ;; Set original orange and yellow to dark rose and green since they conflict
	    ;; with the main text.
	    ("zenburn-orange" . "#CC9999")
	    ("zenburn-yellow" . "#8FB28F")))
    (load-theme 'zenburn t)
    (set-cursor-color "#F0F0F0")
    (set-face-attribute 'region nil :background "#666666")
    ;; Search.
    (set-face-attribute 'isearch nil :background "#ADD8E6" :foreground "#E0115F")
    (set-face-attribute 'lazy-highlight nil :foreground "#E0115F")
    ;; Indent highlight color.
    (set-face-background 'highlight-indent-guides-odd-face "darkgray")
    (set-face-background 'highlight-indent-guides-even-face "dimgray")
    (set-face-foreground 'highlight-indent-guides-character-face "dimgray")
    ;; Mode line.
    (set-face-attribute 'mode-line-buffer-id nil :foreground "#327232" :underline)
    (set-face-attribute 'mode-line-inactive nil :foreground "gray40" :box nil)
    (set-face-attribute 'mode-line nil
			:foreground "#062329"
			:background "#CBB390"
			:box nil)
    ;; Mini buffer current highlighted line color.
    (custom-set-faces '(ivy-current-match ((t (:background "#333333")))))
    ;; Paren matching color.
    (set-face-attribute 'show-paren-match nil :foreground "#E0115F" :background "#CCCCCC"))
#+end_src
* Undo Tree
Emacs default undo/redo sucks.
#+begin_src emacs-lisp
  (use-package undo-tree
    :ensure t
    :defer t
    :diminish undo-tree-mode
    :init
    (global-undo-tree-mode)
    (global-set-key (kbd "C-z") 'undo-tree-undo)
    (global-set-key (kbd "M-z") 'undo-tree-undo) ; Emulate the normal cmd-z.
    (global-set-key (kbd "C-r") 'undo-tree-redo)
    (global-set-key (kbd "M-r") 'undo-tree-redo)
    ;; Somehow you need both to unbind. Why emacs keybinding in modes and package is so confusing?
    (eval-after-load 'undo-tree '(unbind-key (kbd "C-x u")))
    ;; Unbind `C-/` to avoid overwriting for the globally set key mapped to
    ;; comment.
    :bind (:map undo-tree-map
		("C-/" . nil))
    :custom
    (undo-tree-visualizer-diff t)
    (undo-tree-history-directory-alist
       `(("." . ,(expand-file-name ".backup" user-emacs-directory))))
    (undo-tree-visualizer-timestamps t)
    :config
    (unbind-key (kbd "C-x u") undo-tree-map)) ; Unbind visualize key.
#+end_src
* Whitespace
Be tidy please.
#+begin_src emacs-lisp
  (use-package whitespace
    :ensure t
    :init
    (add-hook 'prog-mode-hook 'whitespace-mode)
    (add-hook 'latex-mode-hook 'whitespace-mode)
    (remove-hook 'before-save-hook 'delete-trailing-whitespace)
    :custom
    (whitespace-line-column nil)
    (show-trailing-whitespace t)
    (whitespace-style
     '(face
       ;; tabs spaces trailing space-before-tab space-after-tab
       tabs trailing space-before-tab space-after-tab
       tab-mark)))
#+end_src
* Winner
Restore previous window layouts.
#+begin_src emacs-lisp
(use-package winner
  :ensure t
  :custom
  (winner-boring-buffers
   '("*Completions*"
     "*Compile-Log*"
     "*inferior-lisp*"
     "*Fuzzy Completions*"
     "*Apropos*"
     "*Help*"
     "*cvs*"
     "*Buffer List*"
     "*Ibuffer*"
     "*esh command on file*"))
  :config
  (winner-mode 1))
#+end_src
* Yasnippet
Is it useful?
#+begin_src emacs-lisp
  (use-package yasnippet
    :ensure t
    :diminish yas-minor-mode
    :init
    (use-package yasnippet-snippets :ensure t :after yasnippet)
    :hook ((prog-mode LaTeX-mode org-mode markdown-mode) . yas-minor-mode)
    :bind
    (:map yas-keymap
	  (("TAB" . smarter-yas-expand-next-field)
	   ([(tab)] . smarter-yas-expand-next-field)))
    :config
    (yas-reload-all)
    (defun smarter-yas-expand-next-field ()
      "Try to `yas-expand' then `yas-next-field' at current cursor position."
      (interactive)
      (let ((old-point (point))
	    (old-tick (buffer-chars-modified-tick)))
	(yas-expand)
	(when (and (eq old-point (point))
		   (eq old-tick (buffer-chars-modified-tick)))
	  (ignore-errors (yas-next-field))))))
#+end_src

* My/Disable Useless Things
Why does Emacs put those by default?
#+begin_src emacs-lisp
  (setq inhibit-startup-screen t)
  (setq initial-major-mode 'text-mode)
  (setq inhibit-startup-message t)
  (when (display-graphic-p)
    (tool-bar-mode -1)
    (scroll-bar-mode -1))
  (menu-bar-mode -1)
  (setq-default visible-bell t)
  (blink-cursor-mode 0)
  (setq ns-use-proxy-icon nil) ; Icon of filetype.
  (setq initial-scratch-message "")

  ;; Useless keys.
  (global-unset-key (kbd "C-v"))
  (global-unset-key (kbd "C-t"))
  (global-unset-key (kbd "M-h"))
  (global-unset-key (kbd "C-x C-m"))
  (global-unset-key (kbd "C-x m"))
  (global-unset-key (kbd "C-c m"))
  (global-unset-key (kbd "C-x C-z")) ; Easy to mispress.
  (global-unset-key (kbd "C-x C-u")) ; Easy to mispress.
  (global-unset-key (kbd "C-x C-l")) ; Easy to mispress.
  ;; Too much to type for saving. Map to `C-s` instead.
  (global-unset-key (kbd "C-x C-s"))
#+end_src
* My/Functions
#+begin_src emacs-lisp
  ;; Backward kill word or the region if selected.
  (defun my/backward-kill-word-or-region ()
    "Kill a word backward or the region if selected."
    (interactive)
    (if (region-active-p)
	(kill-region (region-beginning) (region-end))
      (backward-kill-word 1)))

  (global-set-key (kbd "C-w") 'my/backward-kill-word-or-region)

  ;; When splitting window, automatically balance them and switch focus to the
  ;; newly splitted window.
  (global-set-key (kbd "C-x 2")
		  (lambda ()
		    (interactive)
		    (split-window-vertically)
		    (balance-windows)
		    (other-window 1)))
  (global-set-key (kbd "C-x 3")
		  (lambda ()
		    (interactive)
		    (split-window-horizontally)
		    (balance-windows)
		    (other-window 1)))

  ;; Auto-balance window when deleting window.
  (global-set-key (kbd "C-x 0")
		  (lambda ()
		    (interactive)
		    (delete-window)
		    (balance-windows)))

  ;; Toggle column goal.
  (defun my/toggle-goal-column ()
    "Toggle goal column mode."
    (interactive)
    (if goal-column
	(progn
	  (setq goal-column nil)
	  (message "Unset goal column"))
      (progn
	(setq goal-column (current-column))
	(message "Set goal column at %d" goal-column))))

  (global-set-key (kbd "C-x C-u") 'my/toggle-goal-column)

  ;; Reload init files.
  (defun my/reload-init-file ()
    (interactive)
    (load-file user-init-file))

  ;; Save all buffers.
  (defun my/save-all-buffers ()
    "Instead of `save-buffer', save all opened buffers by calling
		`save-some-buffers' with ARG t."
    (interactive)
    (save-some-buffers t))

  (global-set-key (kbd "C-s") 'my/save-all-buffers)

  ;; Mark until a char. A general version of `Zap-up-to-char`.
  (defun my/mark-until-char ()
    "Mark text from current cursor position until the first occurrence
	     of a prompted character."
    (interactive)
    (let ((prompt (read-char "Enter the character: ")))
      (save-excursion
	(search-forward (string prompt) nil t)
	(set-mark (point))
	(goto-char (1- (point)))
	(exchange-point-and-mark))))

  (global-set-key (kbd "M-t") 'my/mark-until-char)

  ;; Backward kill line.
  (defun my/backward-kill-line ()
    (interactive)
    (kill-line 0)
    (indent-according-to-mode))

  (global-set-key (kbd "C-<backspace>") 'my/backward-kill-line) ; Doesn't work in terminal.

  ;; Edit this file.
  (defun my/edit-configs ()
    "Opens this file."
    (interactive)
    (find-file "~/.emacs.d/myinit.org"))

  ;; Show buffer's file path.
  (defun show-file-path ()
    "Show the full path of the file in the minibuffer."
    (interactive)
    (message (buffer-file-name)))

  ;; Kill a word when the cursor is in the middle.
  (defun my/kill-word-at-point ()
    "Kill the word at the current cursor position."
    (interactive)
    (let ((bounds (bounds-of-thing-at-point 'symbol)))
      (when bounds
	(kill-region (car bounds) (cdr bounds)))))

  (global-set-key (kbd "M-w") 'my/kill-word-at-point)

  ;; Stolen from Stevey.
  (defun my/rename-file-and-buffer (new-name)
    "Renames both current buffer and file it's visiting to NEW-NAME."
    (interactive "sNew name: ")
    (let ((name (buffer-name))
	  (filename (buffer-file-name)))
      (if (not filename)
	  (message "Buffer '%s' is not visiting a file!" name)
	(if (get-buffer new-name)
	    (message "A buffer named '%s' already exists!" new-name)
	  (progn
	    (rename-file filename new-name 1)
	    (rename-buffer new-name)
	    (set-visited-file-name new-name)
	    (set-buffer-modified-p nil))))))

  (defun my/move-buffer-file (dir)
    "Moves both current buffer and file it's visiting to DIR."
    (interactive "DNew directory: ")
    (let* ((name (buffer-name))
	   (filename (buffer-file-name))
	   (dir
	    (if (string-match dir "\\(?:/\\|\\\\)$")
		(substring dir 0 -1) dir))
	   (newname (concat dir "/" name)))
      (if (not filename)
	  (message "Buffer '%s' is not visiting a file!" name)
	(progn
	  (copy-file filename newname 1)
	  (delete-file filename)
	  (set-visited-file-name newname)
	  (set-buffer-modified-p nil)  t))))

  ;; Delete this file.
  (defun my/delete-this-file ()
    "Kill the current buffer and deletes the file it is visiting."
    (interactive)
    (let ((filename (buffer-file-name)))
      (if filename
	  (if (y-or-n-p (concat "Do you really want to delete file " filename " ?"))
	      (progn
		(delete-file filename)
		(message "Deleted file %s." filename)
		(kill-buffer)))
	(message "Not a file visiting buffer!"))))


  ;; Unfill paragraph and region. Why aren't they built-in?
  (defun unfill-paragraph ()
    (interactive)
    (let ((fill-column (point-max)))
      (fill-paragraph nil)))

  (defun unfill-region ()
    (interactive)
    (let ((fill-column (point-max)))
      (fill-region (region-beginning) (region-end) nil)))

  ;; C-a and C-e jump to visual line in visual-line-mode.
  (defun my/visual-line-mode-hook ()
    (define-key visual-line-mode-map (kbd "C-a") 'beginning-of-visual-line)
    (define-key visual-line-mode-map (kbd "C-e") 'end-of-visual-line))

  (add-hook 'visual-line-mode-hook 'my/visual-line-mode-hook)
#+end_src
* My/Keybindings
#+begin_src emacs-lisp
  ;; Stevey's suggestion.
  (global-set-key (kbd "C-x t") 'beginning-of-buffer)
  (global-set-key (kbd "C-c t") 'beginning-of-buffer) ; Mispress.
  (global-set-key (kbd "C-x e") 'end-of-buffer)
  (global-set-key (kbd "C-c e") 'end-of-buffer)       ; Mispress.

  ;; Conventional copy/paste.
  (global-set-key (kbd "M-c") 'kill-ring-save)
  (global-set-key (kbd "M-v") 'yank)

  ;; Default word and char movement are always awkward to me.
  (global-set-key (kbd "M-j") 'backward-word)
  (global-set-key (kbd "M-k") 'forward-word)
  (global-set-key (kbd "C-j") 'backward-char)
  (global-set-key (kbd "C-k") 'forward-char)

  ;; C-g is always hard to press.
  (define-key key-translation-map (kbd "ESC") (kbd "C-g"))
  (define-key key-translation-map (kbd "<escape>") (kbd "C-g"))

  ;; Adjust font size like web browsers. Doesn't work in terminal.
  (global-set-key (kbd "C-=") 'text-scale-increase)
  (global-set-key (kbd "C--") 'text-scale-decrease)

  ;; For within-screen jump. No need after avy jump.
  ;; (global-set-key (kbd "M-s") 'isearch-forward)
  ;; (define-key isearch-mode-map (kbd "M-s") 'isearch-repeat-forward)
  ;; (global-set-key (kbd "M-r") 'isearch-backward)
  ;; (define-key isearch-mode-map (kbd "M-r") 'isearch-repeat-backward)

  ;; Cure bad habits.
  (global-set-key (kbd "C-x C-s") (lambda ()
				    (interactive)
				    (message "Use C-s, dude.")))

  (global-set-key (kbd "C-g") (lambda ()
				(interactive)
				(message "Use ESC, dude.")))

  ;; Somehow those two packages aren't installed automatically by `use-package`.
  ;; Need to install them manually first. And then bind them here. And you can't do
  ;; `eval-after-load` for some reason.
  (global-set-key (kbd "C-c C-o") 'dumb-jump-go-other-window)
  (global-set-key (kbd "C-c C-y") 'dumb-jump-back)
  (global-set-key (kbd "C-c b") 'goto-last-change)

  ;; Vim's `i`.
  (use-package change-inner
    :ensure t
    :init
    (global-set-key (kbd "M-i") 'change-inner))

  ;; Misc.
  (global-set-key (kbd "C-o") 'other-window)
  (global-set-key (kbd "M-g") 'goto-line)
  (global-set-key (kbd "C-c DEL") 'delete-blank-lines)
  (global-set-key (kbd "C-c <delete>") 'delete-blank-lines)
  (global-set-key (kbd "C-x k") 'kill-this-buffer) ; Don't ask.
  (global-set-key (kbd "C-c w") 'toggle-truncate-lines) ; Wrap.
  (global-set-key (kbd "C-x q") 'query-replace)
  (global-set-key (kbd "C-x \\") 'sort-lines)
  (global-set-key (kbd "C-x ?") 'describe-key)
  (global-set-key (kbd "C-x c") 'call-last-kbd-macro)

  ;; Aliases.
  (defalias 'rl  'my/reload-init-file)
  (defalias 'cf  'my/edit-configs)
  (defalias 'vce 'vc-ediff)
  (defalias 'rc  'recompile)
  (defalias 'rs  'replace-string)
#+end_src
* My/MacOS
Handle copy/paste in OSX.
#+begin_src emacs-lisp
  (defun copy-from-osx ()
    "Handle copy/paste intelligently on osx."
    (let ((pbpaste (purecopy "/usr/bin/pbpaste")))
      (if (and (eq system-type 'darwin)
	       (file-exists-p pbpaste))
	    (let ((tramp-mode nil)
		  (default-directory "~"))
	      (shell-command-to-string pbpaste)))))

  (defun paste-to-osx (text &optional push)
    (let ((process-connection-type nil))
      (let ((proc (start-process "pbcopy" "*Messages*" "pbcopy")))
	(process-send-string proc text)
	(process-send-eof proc))))

  (if *sys/mac*
      (setq interprogram-cut-function 'paste-to-osx
	    interprogram-paste-function 'copy-from-osx))
#+end_src
* My/Small Configs
#+begin_src emacs-lisp
  ;; C-p, C-n, etc uses visual lines.
  (setq line-move-visual t)

  ;; y or n.
  (fset 'yes-or-no-p 'y-or-n-p)

  ;; Ask before killing emacs.
  (setq confirm-kill-emacs 'y-or-n-p)

  ;; No dialgue!
  (setq use-dialog-box nil)

  ;; Move the backup fies to user-emacs-directory/.backup.
  (setq backup-directory-alist
	`(("." . ,(expand-file-name ".backup" user-emacs-directory))))

  ;; Automatically kill all active processes when closing Emacs.
  (setq confirm-kill-processes nil)

  ;; Turn Off Cursor Alarms.
  (setq ring-bell-function 'ignore)

  ;; Show Keystrokes in Progress Instantly.
  (setq echo-keystrokes 0.1)

  ;; Don't Lock Files.
  (setq-default create-lockfiles nil)

  ;; ad-handle-definition warnings are generated when functions are
  ;; redefined with `defadvice', they are not helpful.
  (setq ad-redefinition-action 'accept)

  ;; Move Custom-Set-Variables to Different File.
  (setq custom-file (concat user-emacs-directory "custom-set-variables.el"))
  (load custom-file 'noerror 'nomessage)

  ;; So Long mitigates slowness due to extremely long lines.
  ;; Currently available in Emacs master branch *only*!
  (when (fboundp 'global-so-long-mode)
    (global-so-long-mode))

  ;; Add a newline automatically at the end of the file upon save.
  (setq require-final-newline t)

  ;; Enable `erase-buffer' function.
  (put 'erase-buffer 'disabled nil)

  ;; Smart tab behavior: indent or complete.
  (setq tab-always-indent 'complete)

  ;; Prevent down-arrow from adding empty lines to the bottom of the buffer.
  (setq next-line-add-newlines nil)

  ;; Don't show line numbers.
  (column-number-mode 1)

  ;; Save whatever’s in the current system clipboard before replacing it with
  ;; the Emacs' text.
  (setq save-interprogram-paste-before-kill t)

  ;; Stop ivy from displaying recentf files.
  (setq ivy-use-virtual-buffers nil)

  ;; Width to wrap lines for `fill-paragraph` and `fill-region`.
  (setq-default fill-column 80)
  (add-hook 'LaTeX-mode-hook 'auto-fill-mode)
  (add-hook 'tex-mode-hook 'auto-fill-mode)
  (add-hook 'text-mode-hook 'auto-fill-mode)
  (add-hook 'org-mode-hook 'auto-fill-mode)

  ;; Visual line don't break the word.
  (setq-default word-wrap t)

  ;; Paren match.
  (add-hook 'prog-mode-hook 'show-paren-mode)

  ;; Automatically update a buffer if a file changes on disk.
  (global-auto-revert-mode 1)
  (add-hook 'dired-mode-hook 'auto-revert-mode) ; Refresh dired too.

  ;; Give context to cursor.
  (setq scroll-margin 4)

  ;; Show unfinished keystrokes early.
  (setq echo-keystrokes 0.1)

  ;; Don't show useless things in mode line.
  (setq display-time-format "[%m-%d (%a) %H:%M]")
  (setq display-time-24hr-format t)
  (setq display-time-default-load-average nil)
  (setq display-time-mail-string "")
  (display-time-mode 1)
  (setq-default mode-line-format
		(list
		 "  "                  ; Don't understand why Emacs puts "-" here.
		 'mode-line-modified
		 "  "
		 'mode-line-buffer-identification
		 "  (%l, %c)  "        ; Buffer name  (line number, column number).
		 'display-time-string  ; Time.
		 "  Focus!"))          ; Of course.

  ;; Don't ask when killing a buffer with a live process.
  (setq kill-buffer-query-functions
	(remq 'process-kill-buffer-query-function
	      kill-buffer-query-functions))

  ;; Tramp.
  (setq tramp-default-method "ssh")

  ;; Uses system trash rather than deleting forever.
  (setq trash-directory "~/.Trash")
  (setq delete-by-moving-to-trash t)

  ;; Compilation.
  (setq-default compilation-always-kill t)
  (setq-default compilation-ask-about-save nil)
  (setq-default compilation-scroll-output t)

  ;; Move `custom-set-variables` to a different file.
  (setq custom-file (concat user-emacs-directory "custom-set-variables.el"))
  (load custom-file 'noerror)

  ;; Switch to help buffers automatically.
  (setq help-window-select t)

  ;; Reduce a little typing latency.
  (setq redisplay-dont-pause t)

  ;; Electric indent.
  (add-hook 'prog-mode-hook 'electric-indent-mode)
  ;; Cause annoying auto indent in org mode.
  (add-hook 'org-mode-hook (lambda () (electric-indent-local-mode 0)))

  ;; Warn only when opening files bigger than 100MB.
  (setq large-file-warning-threshold 100000000)

  ;; Frame title shows either a file or a buffer name
  ;; (if the buffer isn't visiting a file)
  (setq frame-title-format
	'((:eval (if (buffer-file-name)
		     (abbreviate-file-name (buffer-file-name))
		   "%b"))))

  ;; Set frame size and position.
  (when (display-graphic-p)
    (add-to-list 'default-frame-alist '(fullscreen . maximized)))

  ;; Treat selected region like a normal region in other systems.
  (delete-selection-mode t)

  ;; Savehist.
  (setq history-length 100)
  (savehist-mode 1)

  ;; Column width indicator.
  (setq-default display-fill-column-indicator-column 79) ; 80 linewidth
  (add-hook 'prog-mode-hook 'display-fill-column-indicator-mode)

  ;; https://martinfowler.com/articles/2023-xref-problem.html.
  (setq dumb-jump-force-searcher 'rg)

  ;; Sticky function head.
  (use-package semantic
    :ensure nil
    :hook
    (prog-mode . semantic-mode)
    (prog-mode . global-semantic-stickyfunc-mode))

  ;; Default some files to text-mode.
  (add-to-list 'auto-mode-alist '("\\.in\\'" . text-mode))
  (add-to-list 'auto-mode-alist '("\\.out\\'" . text-mode))
  (add-to-list 'auto-mode-alist '("\\.args\\'" . text-mode))
  (add-to-list 'auto-mode-alist '("\\.bb\\'" . shell-script-mode))
  (add-to-list 'auto-mode-alist '("\\.bbclass\\'" . shell-script-mode))
  (add-to-list 'auto-mode-alist '("\\.Rmd\\'" . markdown-mode))
#+end_src
